plugins {
    id 'java'
    id "io.papermc.paperweight.userdev" version "1.7.2-SNAPSHOT"
    id "xyz.jpenilla.run-paper" version "2.3.0"

    id "io.freefair.lombok" version "6.2.0"
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

group = 'org.totemcraft'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = "https://maven.pkg.github.com/theTd/tclite"
        mavenContent {
            includeGroup("com.mineclay")
            includeGroup("com.mineclay.tclite")
        }
        credentials {
            username = "$gprUser"
            password = "$gprKey"
        }
    }
}

dependencies {
    paperweight.paperDevBundle("1.18.2-R0.1-SNAPSHOT")
    implementation("org.jooq:joor-java-8:0.9.15")
    implementation("xyz.jpenilla:reflection-remapper:0.1.1")
    implementation("com.mineclay:tclite:1.7.0-SNAPSHOT") {
        exclude(group: "com.github.cryptomorin")
    }
//    compileOnly "io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.named("assemble").configure {
    dependsOn("reobfJar")
}

tasks.shadowJar.configure {
    minimize()
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
